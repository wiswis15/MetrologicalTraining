find_package(PkgConfig)
pkg_check_modules(PC_WPEFRAMEWORK WPEFramework)

find_package(BcmNxServer)
find_package(VirtualInput)

find_library(
   WPEFRAMEWORKPLUGINS_LIBRARY
   NAMES WPEFrameworkPlugins
   HINTS ${PC_WPEFRAMEWORK_LIBDIR} ${PC_WPEFRAMEWORK_LIBRARY_DIRS}
)

find_path(
   WPEFRAMEWORKPLUGINS_INCLUDE_DIR
   WPEFramework/plugins/plugins.h
   HINTS ${PC_WPEFRAMEWORK_INCLUDEDIR} ${PC_WPEFRAMEWORK_INCLUDE_DIRS}
)

set(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_LIBS
    ${WPEFRAMEWORKPLUGINS_LIBRARY}
)

set(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_SOURCES 
    Module.cpp
    RemoteControl.cpp
    RemoteAdministrator.cpp
)

if(LIBVIRTUAL_INPUT_FOUND)
    list(APPEND WPEFRAMEWORK_PLUGIN_REMOTECONTROL_DEFINITIONS -DVIRTUAL_INPUT_ENABLED)
endif(LIBVIRTUAL_INPUT_FOUND)

if(LIBNXSERVER_FOUND)
    include_directories(${LIBNXCLIENT_INCLUDE_DIRS})
    list(APPEND WPEFRAMEWORK_PLUGIN_REMOTECONTROL_LIBS ${LIBNXCLIENT_LIBRARY})

    if (WPEFRAMEWORK_PLUGIN_REMOTECONTROL_IRNEXUS)
        list(APPEND WPEFRAMEWORK_PLUGIN_REMOTECONTROL_SOURCES IRRemote.cpp)
        list(APPEND WPEFRAMEWORK_PLUGIN_REMOTECONTROL_DEFINITIONS -DIR_ENABLED)

        if(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_HOST)
            set(IRKEY_MAP_FILE "${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_HOST}-IRRemoteKeyMap-${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_IRMODE}.json")
        else(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_HOST)
            set(IRKEY_MAP_FILE "IRRemoteKeyMap-${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_IRMODE}.json")
        endif(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_HOST)

        if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/KeyMaps/${IRKEY_MAP_FILE}")
            message("Nexus IR device key map file: " ${IRKEY_MAP_FILE})
            install(FILES KeyMaps/${IRKEY_MAP_FILE} DESTINATION ${CMAKE_INSTALL_PREFIX}/share/WPEFramework/RemoteControl RENAME "NexusIR.json")
        endif()
    endif (WPEFRAMEWORK_PLUGIN_REMOTECONTROL_IRNEXUS)
endif(LIBNXSERVER_FOUND)

# For future RF integration
if (WPEFRAMEWORK_PLUGIN_REMOTECONTROL_RF)
    if(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_HOST)
        set(RFKEY_MAP_FILE "${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_HOST}-RFRemoteKeyMap.json")
    else(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_HOST)
        set(RFKEY_MAP_FILE "RFRemoteKeyMap.json")
    endif(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_HOST)

    if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/KeyMaps/${RFKEY_MAP_FILE}")
        message("RF device key map file: " ${RFKEY_MAP_FILE})
        install(FILES KeyMaps/${RFKEY_MAP_FILE} DESTINATION ${CMAKE_INSTALL_PREFIX}/share/WPEFramework/RemoteControl RENAME "RemoteRF.json" )
    endif()
endif(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_RF)

message ("LIBS TO INCLUDE: [ ${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_LIBS} ]")
install(FILES KeyMaps/keymap.json DESTINATION ${CMAKE_INSTALL_PREFIX}/share/WPEFramework/RemoteControl)
add_definitions (${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_DEFINITIONS})
 
# add the library
add_library(WPEFrameworkRemoteControl SHARED ${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_SOURCES})
include_directories(${WPEFRAMEWORKPLUGINS_INCLUDE_DIR})
target_link_libraries(WPEFrameworkRemoteControl ${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_LIBS})
set_target_properties(WPEFrameworkRemoteControl PROPERTIES DEFINE_SYMBOL WPEFRAMEWORK_PLUGIN_REMOTECONTROL)

install(TARGETS WPEFrameworkRemoteControl DESTINATION ${CMAKE_INSTALL_PREFIX}/lib/wpeframework/plugins)

# default configutation
map() 
#  kv(callsign RemoteControl)
  kv(locator libWPEFrameworkRemoteControl.so)
  kv(classname RemoteControl)
  key(configuration)
  map()
    kv(mapfile keymap.json)
    kv(repeatstart 500)
    kv(repeatinterval 100)
  end()
  ans(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_CONFIGURATION)
end()
ans(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_CONFIG)

if (WPEFRAMEWORK_PLUGIN_REMOTECONTROL_IRNEXUS)
    map()
    kv(device NexusIR)
    kv(mapfile NexusIR.json)
    kv(passon false)
    end()
    ans(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_NEXUS_DEVICE)

if (WPEFRAMEWORK_PLUGIN_REMOTECONTROL_CODEMASK)
    map_append(${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_NEXUS_DEVICE} codemask ${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_CODEMASK})
endif(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_CODEMASK)

    map_append(${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_CONFIGURATION} specific ___array___)
    map_append(${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_CONFIGURATION} specific ${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_NEXUS_DEVICE})
endif(WPEFRAMEWORK_PLUGIN_REMOTECONTROL_IRNEXUS)

json_write("${CMAKE_CURRENT_LIST_DIR}/RemoteControl.json" ${WPEFRAMEWORK_PLUGIN_REMOTECONTROL_CONFIG})
install(FILES RemoteControl.json DESTINATION ${CMAKE_INSTALL_PREFIX}/../etc/WPEFramework/plugins/)

